#pragma kernel HeightMap
#pragma kernel NormalMap

#include "Simplex.compute"

uint _Seed;
uint _Width;
uint _Height;

int _Octaves = 16;
float _SelfSimilarity = 1.0;
float _Frequency = 1.0;
float _Amplitude = 1.0;
float _Lacunarity = 2.0;
float _Warp = 0;

RWTexture2D<float4> _HeightMap;
RWTexture2D<float4> _NormalMap;


// Use flat plane to get a nice flat area that can be displaced by a really low frequency, low amplitude height map
// Then, intersect the plane with a higher amplitude height map
// Scan the two maps, and interpolate the intersections such that they smoothly transition

struct NoiseSpecifications {
    float3 pos;
    int octaves;
    float gain;
    float frequency;
    float amplitude;
    float lacunarity;
    float warp;
};

float4 fbm(NoiseSpecifications specs) {    
    float noise = 0.0;
    float3 dsum = 0;

    for (int i = 0; i < specs.octaves; ++i) {
        float4 n = snoise_grad((specs.pos + specs.warp * dsum) * specs.frequency) / (1.0 + dot(dsum, dsum));
        dsum += n.xyz;
        noise += specs.amplitude * n.w;
        specs.frequency *= specs.lacunarity;
        specs.amplitude *= specs.gain;
    }

    return float4(noise, dsum);
}

float4 height(uint3 v) {
    NoiseSpecifications n;
    n.pos = float3(v.x, v.y, _Seed);
    n.octaves = _Octaves;
    n.gain = exp2(-_SelfSimilarity);
    n.frequency = _Frequency;
    n.amplitude = _Amplitude;
    n.lacunarity = _Lacunarity;
    n.warp = _Warp;

    NoiseSpecifications n2;
    n2.pos = n.pos;
    n2.octaves = _Octaves;
    n2.gain = n.gain;
    n2.frequency = _Frequency;
    n2.amplitude = _Amplitude / 4;
    n2.lacunarity = _Lacunarity;
    n2.warp = _Warp;

    float4 h = fbm(n);
    return h.x > 0 ? h : fbm(n2);
}

[numthreads(8,8,1)]
void HeightMap(uint3 id : SV_DISPATCHTHREADID) {
    _HeightMap[id.xy] = height(id);
}

// Unused, calculate normal map by central difference
[numthreads(8,8,1)]
void NormalMap(uint3 id : SV_DISPATCHTHREADID) {
    float u1 = (id.x == 1)           ? height(id - uint3(1, 0, 0)).x : _HeightMap[id.xy - uint2(1, 0)].r;
    float u2 = (id.x == _Width - 2)  ? height(id + uint3(1, 0, 0)).x : _HeightMap[id.xy + uint2(1, 0)].r;
    float v1 = (id.y == 1)           ? height(id - uint3(0, 1, 0)).x : _HeightMap[id.xy - uint2(0, 1)].r;
    float v2 = (id.y == _Height - 2) ? height(id + uint3(0, 1, 0)).x : _HeightMap[id.xy + uint2(0, 1)].r;

    float3 normal = float3(u1 - u2, 1, v1 - v2);

    if (id.x == 0 || id.x == _Width - 1 || id.y == 0 || id.y == _Height - 1)
        normal = float3(0, 1, 0);

    normal = normalize(normal);
    normal.xyz = (normal.xyz + 1) / 2;

    _NormalMap[id.xy] = float4(normal.x, normal.z, normal.y, 0);
}